#!/usr/bin/perl

use strict;
use warnings;
use File::Slurp;                # perl-File-Slurp

sub enclosure_logical_id {
    my $devpath = shift;
    $devpath =~ m{(.*(end_device-[\d:]+))/target}
        or die "$devpath: unrecognized\n";
    chomp( my $enclosure_identifier = read_file("/sys$1/sas_device/$2/enclosure_identifier") );
    return $enclosure_identifier;
}

sub slot {
    my $devpath = shift;
    $devpath =~ m{(.*)/block/sd[^/]+$} or die "$devpath: unrecognized\n";
    for ( 1 .. 10 ) {
        if ( my ($slot) = </sys$1/enclosure_device:*S[Ll][Oo][Tt]*> ) {
            if ( $slot =~ /Slot 0*(\d+)/ ) {
                return $1 - 1;  # Supermicro SAS2x
            }
            elsif ( $slot =~ /Slot0*(\d+)/ ) {
                return $1;      # Supermicro SAS3x
            }
            elsif ( $slot =~ /SLOT (\d+),/ ) {
                return $1;	# HGST H4060-J
            }
            elsif ( $slot =~ /Drive Slot #(\d+)_/ ) {
                return $1;	# HGST JBOD
            }
            return;
        }
        sleep 3;
    }
    return;
}

# for debug
unless ( defined $ENV{"DEVPATH"} ) {
    defined $ARGV[0] or die "Usage: $0 <sd[a-z]+ | dm-[0-9]+>";
    open my $udevadm, '-|', qw/udevadm info -q property -n/, $ARGV[0] or die;
    while (<$udevadm>) {
        chomp;
        my ( $key, $value ) = split /=/;
        $ENV{$key} = $value unless exists $ENV{$key};
    }
    close $udevadm;
}

# main
if ( defined $ENV{"DM_NAME"} ) {
    exit unless defined $ENV{"MPATH_SBIN_PATH"};
    opendir my $dh, "/sys$ENV{DEVPATH}/slaves" or exit;
    for my $name ( grep {/\Asd/} readdir($dh) ) {
        my $devpath = `udevadm info -q path -n $name`;
        exit unless $devpath =~ /expander/;
        my $enclosure_logical_id = enclosure_logical_id($devpath);
        my $slot                 = slot($devpath);
        if ( defined $enclosure_logical_id && defined $slot ) {
            printf "ID_VDEV=%s:%02d\n",                   $enclosure_logical_id, $slot;
            printf "ID_VDEV_PATH=disk/by-vdev/%s:%02d\n", $enclosure_logical_id, $slot;
            last;
        }
    }
}
else {
    exit if ( defined $ENV{"DM_MULTIPATH_DEVICE_PATH"} && $ENV{"DM_MULTIPATH_DEVICE_PATH"} == 1 );
    exit unless $ENV{"DEVPATH"} =~ /expander/;
    my $enclosure_logical_id = enclosure_logical_id( $ENV{"DEVPATH"} );
    my $slot                 = slot( $ENV{"DEVPATH"} );
    if ( defined $enclosure_logical_id && defined $slot ) {
        printf "ID_VDEV=%s:%02d\n",                   $enclosure_logical_id, $slot;
        printf "ID_VDEV_PATH=disk/by-vdev/%s:%02d\n", $enclosure_logical_id, $slot;
    }
}
